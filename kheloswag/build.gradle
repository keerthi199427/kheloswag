buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {		
		classpath('org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE')
		classpath('io.swagger:swagger-codegen:2.3.1')
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.myworks.creation'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

bootJar {
    mainClassName = "com.myworks.creation.kheloswag.KheloswagApplication"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.hsqldb:hsqldb'
	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'
	implementation 'io.springfox:springfox-bean-validators:2.9.2'
	implementation 'mysql:mysql-connector-java'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compileOnly 'org.projectlombok:lombok:1.18.10'
}


import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

def swaggerInput = "${project.projectDir}/apiDefintion/kheloIndiaAPI.yaml"
def swaggerOutputDir = 'src/main/java'


task generateAPI {

	inputs.file(swaggerInput)
	outputs.dir(swaggerOutputDir)
	doLast {
		def config = new CodegenConfigurator()
		config.setInputSpec(swaggerInput)
		config.setOutputDir(project.projectDir.toString())
		config.setLang('spring')
		config.setAdditionalProperties ([
				'invokerPackage': 'com.myworks.creation.kheloswag',
				'modelPackage': 'com.myworks.creation.kheloswag.model',
				'apiPackage': 'com.myworks.creation.kheloswag.controller',
				'dateLibrary': 'java8',
				'useTags': 'true',
				'interfaceOnly': 'true',
				'useBeanValidation' : 'true',
				'delegatePattern': 'true',
				'hideGenerationTimestamp' :'true'])
		new DefaultGenerator().opts(config.toClientOptInput()).generate()
	}
}
